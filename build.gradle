import org.jetbrains.gradle.ext.Gradle

plugins {
    id 'java'
    id 'java-library'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
    id 'com.gtnewhorizons.retrofuturagradle' version '1.3.27'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "me.modmuss50.mod-publish-plugin" version "0.5.1"
}

version = "${minecraft_version}-${mod_version}"
group = project.maven_group

base {
    archivesName = mod_archive_name
}

// Set the toolchain version to decouple the Java we run Gradle with from the Java used to compile and run the mod
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
        // Azul covers the most platforms for Java 8 toolchains, crucially including MacOS arm64
    }
    // Generate sources and javadocs jars when building and publishing
    withSourcesJar()
    // withJavadocJar()
}

compileJava {
    sourceCompatibility = 17 // for the IDE support
    options.release = 8

    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

minecraft {
    mcVersion = '1.12.2'

    // MCP Mappings
    mcpMappingChannel = 'stable'
    mcpMappingVersion = '39'

    // Set username here, the UUID will be looked up automatically
    username = 'Developer'

    // Add any additional tweaker classes here
    // extraTweakClasses.add('org.spongepowered.asm.launch.MixinTweaker')

    // Add various JVM arguments here for runtime
    def args = ["-ea:${project.group}"]
    if (project.use_coremod.toBoolean()) {
        args << '-Dfml.coreMods.load=' + coremod_plugin_class_name
    }
    if (project.use_mixins.toBoolean()) {
        args << '-Dmixin.hotSwap=true'
        args << '-Dmixin.checks.interfaces=true'
        args << '-Dmixin.debug.export=true'
    }
    extraRunJvmArguments.addAll(args)

    // Include and use dependencies' Access Transformer files
    useDependencyAccessTransformers = true

    // Add any properties you want to swap out for a dynamic value at build time here
    // Any properties here will be added to a class at build time, the name can be configured below
    // Example:
    // injectedTags.put('VERSION', project.version)
    // injectedTags.put('MOD_ID', project.mod_id)
    injectedTags.put("MOD_VERSION", project.mod_version)
}

tasks.injectTags.outputClassName.set("${project.mod_id}.Tags")


repositories {
    flatDir { dir 'libs' }
    maven {
        name 'CleanroomMC Maven'
        url 'https://maven.cleanroommc.com'
    }
    maven {
        name 'SpongePowered Maven'
        url 'https://repo.spongepowered.org/maven'
    }
    maven {
        name 'CurseMaven'
        url 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        name = "CoFH Maven"
        url = "https://maven.covers1624.net"
    }
    maven {
        name = "MC Mod Devs"
        url = 'https://maven.mcmoddev.com'
    }
    maven {
        url = 'https://maven.blamejared.com'
        name = 'BlameJared Maven'
    }

    mavenLocal() // Must be last for caching to work
}

dependencies {
    if (project.use_assetmover.toBoolean()) {
        implementation 'com.cleanroommc:assetmover:2.5'
    }
    if (project.use_mixins.toBoolean()) {
        implementation 'zone.rong:mixinbooter:7.1'
    }

    // Example of deobfuscating a dependency
    // implementation rfg.deobf('curse.maven:had-enough-items-557549:4543375')

    if (project.use_mixins.toBoolean()) {
        // Change your mixin refmap name here:
        String mixin = modUtils.enableMixins('org.spongepowered:mixin:0.8.3', "mixins.${project.mod_id}.refmap.json")
        api(mixin) {
            transitive = false
        }
        annotationProcessor 'org.ow2.asm:asm-debug-all:5.2'
        annotationProcessor 'com.google.code.gson:gson:2.8.6'
        annotationProcessor(mixin) {
            transitive = false
        }
    }
    implementation fileTree(dir: 'libs', include: ['*.jar'])


    implementation "curse.maven:matter-overdrive-community-edition-557428:5114422"
    implementation "curse.maven:had-enough-items-557549:5210315"
    implementation "net.darkhax.tesla:Tesla-1.12.2:${tesla_version}"
    implementation "curse.maven:simpledifficulty-for-underdog-574162:4651150"
    implementation "cofh:RedstoneFlux:1.12-2.0.2.3:universal"
    implementation "vazkii.botania:Botania:${botania_version}"

    compileOnly "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.698"

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    //implementation "com.google.guava:guava:33.0.0-jre"


    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '24.1.0'

    annotationProcessor 'com.pkware.jabel:jabel-javac-plugin:1.0.1-1'
    compileOnly 'com.pkware.jabel:jabel-javac-plugin:1.0.1-1'

    

}
// Adds Access Transformer files to tasks
if (project.use_access_transformer.toBoolean()) {
    for (File at : sourceSets.getByName("main").resources.files) {
        if (at.name.toLowerCase().endsWith("_at.cfg")) {
            tasks.deobfuscateMergedJarToSrg.accessTransformerFiles.from(at)
            tasks.srgifyBinpatchedJar.accessTransformerFiles.from(at)
        }
    }
}

processResources {
    // This will ensure that this task is redone when the versions change
    inputs.property 'version', project.mod_version
    inputs.property 'mcversion', project.minecraft_version

    // Replace various properties in mcmod.info and pack.mcmeta if applicable
    filesMatching(['mcmod.info', 'pack.mcmeta']) { fcd ->
        // Replace version and mcversion
        fcd.expand(
                'mod_version': project.mod_version,
                'minecraft_version': project.minecraft_version
        )
    }


    if (project.use_access_transformer.toBoolean()) {
        rename '(.+_at.cfg)', 'META-INF/$1' // Make sure Access Transformer files are in META-INF folder
    }
}

tasks.register('generateMetaFiles') {
    // Code for execution after the whole buildscript was parsed and loaded
    doLast {
        // Clear the dependencyMeta directory since we don't want old dependencies to still be listed in there
        file("${buildDir}/dependencyMeta/").deleteDir()
        configurations.embed.resolvedConfiguration.resolvedArtifacts.each {
            // Create a meta file for each dependency in a specified directory
            def metaFile = file("${buildDir}/dependencyMeta/${it.file.name}.meta")
            metaFile.parentFile.mkdirs()
            // Use the Gradle notation provided by the API ('group:artifact:version') for the meta file...
            def artifactRef = it.moduleVersion.toString()
            // ...and append the classifier if present
            if (it.classifier != null) {
                artifactRef += ":${it.classifier}"
            }
            // Write the artifact information to the meta file, to be used by the
            metaFile.text = "Maven-Artifact: $artifactRef"
        }
    }
}

jar {
    exclude "**/TODO.txt"
    exclude "**/update-dist.json"
    //destinationDir = file 'output'

    manifest {
        attributes FMLAT: "cyberware_at.cfg"
    }
}




tasks.named("processIdeaSettings").configure {
    dependsOn("injectTags")
}

publishMods {
    file = jar.archiveFile
    modLoaders.add("forge")
    changelog = ""//file("changelog.txt").text
    type = STABLE

    curseforge {
        accessToken = ""//file("Key.txt").text
        projectId = "580404"
        minecraftVersions.add("1.12.2")


        optional { slug = "apex-guns" }

    }
}
